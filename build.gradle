/*
 * Copyright 2019 Szymon Micyk
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
    id 'groovy'
    id 'idea'
    id 'eclipse'
    id 'ivy-publish'
    id 'codenarc'
    id 'pl.allegro.tech.build.axion-release' version '1.12.1'
}

repositories {
    mavenCentral()
}

scmVersion {
    tag {
        prefix = 'v'
        initialVersion = { config, position -> '0.8.0' }
    }
}

project.version = scmVersion.version

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

dependencies {
    implementation('org.codehaus.groovy:groovy-all:2.5.9')

    implementation('org.apache.jmeter:ApacheJMeter_core:5.1.1') {
        exclude group: 'xerces', module: 'xercesImpl'
        exclude group: 'xml-apis', module: 'xml-apis'
    }

    implementation('org.apache.jmeter:ApacheJMeter_java:5.1.1') {
        exclude group: 'xerces', module: 'xercesImpl'
        exclude group: 'xml-apis', module: 'xml-apis'
    }

    implementation('org.apache.jmeter:ApacheJMeter_components:5.1.1') {
        exclude group: 'xerces', module: 'xercesImpl'
        exclude group: 'xml-apis', module: 'xml-apis'
    }

    implementation('org.apache.jmeter:ApacheJMeter_http:5.1.1') {
        exclude group: 'xerces', module: 'xercesImpl'
        exclude group: 'xml-apis', module: 'xml-apis'
        exclude group: 'javax.ws.rs', module: 'javax.ws.rs-api'
    }

    implementation('org.apache.jmeter:ApacheJMeter_functions:5.1.1') {
        exclude group: 'xerces', module: 'xercesImpl'
        exclude group: 'xml-apis', module: 'xml-apis'
    }

    implementation('javax.ws.rs:javax.ws.rs-api:2.1.1@jar')

    // testing and mocking
    testCompile('junit:junit:4.12')
    testCompile('org.spockframework:spock-core:1.3-groovy-2.5')

    testCompile('org.mock-server:mockserver-netty:5.6.0')
    testCompile('org.mock-server:mockserver-client-java:5.6.0')

    testCompile('org.xmlunit:xmlunit-core:2.6.3')
}

codenarc {
    toolVersion = '1.4'
    configFile = file("${rootProject.projectDir}/config/codenarc/rules.groovy")
    reportFormat = 'html'
    ignoreFailures = true
}

//* Test configuration *//
tasks.withType(Test) {
    testLogging {
        events 'started', 'passed'
    }
}

//sourceSets {
//    groovydoc {
//        resources {
//            srcDir 'src/groovydoc'
//        }
//    }
//}

groovydoc {
    docTitle 'Groovy JMeter DSL Specification'
    overviewText project.resources.text.fromFile('src/groovydoc/resources/overview-summary.html')
    windowTitle 'Groovy JMeter DSL'
    footer 'Szymon Micyk'
}

publishing {
    repositories {
        ivy {
            artifactPattern "${System.properties['user.home']}/.groovy/grapes/[organization]/[module]/jars/[artifact]-[revision](.[ext])"
            ivyPattern "${System.properties['user.home']}/.groovy/grapes/[organization]/[module]/ivy-[revision].xml"
        }
    }
    publications {
       ivy(IvyPublication) {
            organisation = 'net.simonix.scripts'
            module = 'groovy-jmeter'
            revision = project.version

            descriptor.status = 'release'

            from components.java
        }
    }
}
